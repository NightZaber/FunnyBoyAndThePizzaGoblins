// room script file

function hHotspot1_AnyClick()
{
  cMavish.Say("&2 My dead partner, Miller.");
}

function hHotspot2_AnyClick()
{
  cMavish.Say("&3 My favorite slice of pizza.");
}

function hHotspot3_AnyClick()
{
  cMavish.Say("&4 Maybe one day I'll find the missing link and finally put this case to rest.");
}

function hHotspot4_Look()
{
  cMavish.Say("&5 Mmm... Comfy chair...");
}

function hHotspot4_Interact()
{
  cMavish.Say("&6 I don't have time to sit down and get swamp balls.");
}
function Office_Sandwich_Look()
{
  cMavish.Say("&7 Mmm... It's a sandwich.");
}

function Office_Sandwich_PickUp()
{
  cMavish.Walk(230,140, eBlock, eWalkableAreas);
  cMavish.FaceDirection(eDirectionUp);
  Display("You take the sandwich out of the refrigerator.");
  cMavish.AddInventory(Sandwich);
  OfficeSandwich_Taken = 1;
  Office_Sandwich.Visible = false;
}

function Office_FridgeDoor_Look()
{
  cMavish.Say("&8 It's my miniature fridge.");
}

function Office_FridgeDoor_Interact()
{
  cMavish.Walk(230,140, eBlock, eWalkableAreas);
  cMavish.FaceDirection(eDirectionUp);
  aFridge_door_open.Play();
  Display("You open the door to the refrigerator.");
  Office_FridgeDoor.Visible = false;
  fridge_magnet.Visible = false;
  Office_FridgeDoor2.Visible = true;
  if (OfficeSandwich_Taken == 0)
    {
      Office_Sandwich.Visible = true;
    }
}

function Office_FridgeDoor2_Interact()
{
  cMavish.Walk(230,140, eBlock, eWalkableAreas);
  cMavish.FaceDirection(eDirectionUp);
  aFridge_door_close.Play();
  Display("You close the door to the refrigerator.");
  Office_Sandwich.Visible = false;
  Office_FridgeDoor.Visible = true;
  Office_FridgeDoor2.Visible = false;
  if (cMavish.HasInventory(Magnet))
  {}
  else fridge_magnet.Visible = true;
}

function hHotspot5_AnyClick()
{
  cMavish.FaceLocation(160,  80);
  cMavish.Say("&10 Ah, it's the window.");
}

function hHotspot6_AnyClick()
{
  cMavish.Say("&12 At the end of a long day, at least I can take solace in knowing my desk will always be here.");
}

function hHotspot6_UseInv()
{
  if (cMavish.ActiveInventory == Sandwich)
    {
      cMavish.Say("&13 I don't think my desk is very hungry right now.");
    }
}

function mofficedoor_Look()
{
  if (MAVISHROOM_DOORLOCKED == true)
    {
        cMavish.Say("&14 Oh shit. I think I'm locked in here.");
    }
  else
    {
        cMavish.Say("&15 I think the door is unlocked now.");
    }
}

function room_Load()
{
  //Set The Doors and Walkable Areas
  mofficedoor.Visible = true;
  mofficedoor2.Visible = false;
  RemoveWalkableArea(2);
  
}

function mofficedoor_Interact()
{
  if (MAVISHROOM_DOORLOCKED == true)
    {
        cMavish.Walk(52,  161, eBlock, eWalkableAreas);
        cMavish.FaceDirection(eDirectionLeft);
        aDoor_locked.Play();
        Wait(80);
        cMavish.Say("&14 Oh shit. I think I'm locked in here.");
        if (KeyDrop == false)
          {
            cMavish.Say("&16 I think the key is stuck in the other side of the door.");
          }
    }
  else
    {
        cMavish.Walk(52,  161, eBlock, eWalkableAreas);
        cMavish.FaceDirection(eDirectionLeft);
        Display("You open the door.");
        aDoor_move.Play();
        mofficedoor.Visible = false;
        mofficedoor2.Visible = true;
        RestoreWalkableArea(2);
    }
}

function mofficedoor_UseInv()
{
  if (KeyDrop == false)
    {
      if (cMavish.ActiveInventory == Brick)
        {
          cMavish.Walk(52,  161, eBlock, eWalkableAreas);
          cMavish.FaceDirection(eDirectionLeft);
          aHAMMER_SHOES.Play();
          Wait(40);
          Display("You bash the door handle with your brick.");
      
          aDoor_unlock.Play();
          cMavish.Say("&17 It sounds the key fell out of the door.");
          KeyDrop = true;
        }
      else if (cMavish.ActiveInventory == Magnet)
        {
          cMavish.Walk(52,  161, eBlock, eWalkableAreas);
          cMavish.FaceDirection(eDirectionLeft);
          
          cMavish.Say("&18 I don't have to use this item here...");
          cMavish.Say("&19 ...yet.");
        }
    }
  else if (KeyDrop == true)
    {
      if (cMavish.ActiveInventory == Brick)
        {
          cMavish.Walk(52,  161, eBlock, eWalkableAreas);
          cMavish.FaceDirection(eDirectionLeft);
          cMavish.Say("&20 I already knocked the key out of place.");
        }
      else if ((cMavish.ActiveInventory == Magnet) &! (cMavish.HasInventory(Key)))
        {
          cMavish.Walk(52,  161, eBlock, eWalkableAreas);
          cMavish.FaceDirection(eDirectionLeft);
          aMagnet.Play();
          Display("Using the magnet, you suck the key out from under the door and put it in your inventory!");
          cMavish.Say("&21 Where there's a key, there's glee.");
          cMavish.AddInventory(Key);
        }
    }
    
   if ((cMavish.ActiveInventory == Key) && (MAVISHROOM_DOORLOCKED == true))
    {
      cMavish.Walk(52,  161, eBlock, eWalkableAreas);
      cMavish.FaceDirection(eDirectionLeft);
      aDoor_unlock.Play();
      Display("You unlock the door with the key!");
      cMavish.Say("&22 Crisis averted.");
      MAVISHROOM_DOORLOCKED = false;
    }
   else if ((cMavish.ActiveInventory == Key) && (MAVISHROOM_DOORLOCKED == false))
    {
      cMavish.Walk(52,  161, eBlock, eWalkableAreas);
      cMavish.FaceDirection(eDirectionLeft);
      
      cMavish.Say("&23 You think I'm about to lock myself back in here?");
      cMavish.Say("&24 Man. Imagine being that retarded.");
    }
}

function fridge_magnet_Look()
{
  cMavish.Say("&25 It's my fridge magnet. Miller's wife gave it to me as a memento.");
}

function fridge_magnet_Interact()
{
  cMavish.Walk(230,140, eBlock, eWalkableAreas);
  cMavish.FaceDirection(eDirectionUp);
  
  Display("You take the magnet from the refrigerator.");
  cMavish.AddInventory(Magnet);
  fridge_magnet.Visible = false;
}

function mofficedoor2_Interact()
{

        cMavish.Walk(52,  161, eBlock, eWalkableAreas);
        cMavish.FaceDirection(eDirectionLeft);
        Display("You close the door.");
        aDoor_move.Play();
        mofficedoor.Visible = true;
        mofficedoor2.Visible = false;
        RemoveWalkableArea(2);

}

function region1_WalksOnto()
{
  cMavish.ChangeRoom(3,  393, 160, eDirectionLeft);
}

function room_FirstLoad()
{
  //Play The Ambient Sound
  aOffice_sound.Play(eAudioPriorityNormal, eRepeat);
}
